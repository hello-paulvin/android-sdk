// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'version.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }

    dependencies {
        classpath "gradle.plugin.com.browserstack.gradle:browserstack-gradle-plugin:2.3.3"
        classpath 'com.android.tools.build:gradle:4.1.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    versionCode = VERSIONCODE
    versionName = VERSIONNAME

    javaCompatVersion = JavaVersion.VERSION_1_8

    minSdkVersion = 21
    compileSdkVersion = 30
    targetSdkVersion = 30
    buildToolsVersion = "29.0.2"
    packagecloudWagonVersion = "0.0.6"

    gsonVersion = "2.8.6"
    bumptechGlideVersion = "4.11.0"

    androidMaterialVersion = "1.3.0-alpha03"
    androidxBrowserVersion = "1.2.0"
    androidxTestCoreVersion = "1.3.0"
    androidxTestRunnerVersion = "1.3.0"
    androidxTestRulesVersion = "1.3.0"
    androidxTestEspressoVersion = "3.3.0"
    androidxTestJunitVersion = "1.1.2"
    androidxTestUIAutomatorVersion = "2.2.0"

    rxjavaVersion = "1.3.8"
    rxandroidVersion = "1.2.1"

    mockitoCoreVersion = "2.23.4"
    robolectricVersion = "4.4"
    runnerVersion = "1.0.2"
    rulesVersion = "1.0.2"

    jsonsnapshotVersion = "1.0.17"
    harmcrestVersion = "2.2"
}

task buildci(type: GradleBuild) {
    if (versionName.endsWith('SNAPSHOT')) {
        // tasks for SNAPSHOT builds
        printf "Task for SNAPSHOT builds, uploading only to Nexus."
        tasks = [
                'clean',
                ':payment:test',
                ':payment:assemble',
                ':example-sdk:runFunctionalTest',
                ':example-shop:runFunctionalTest',
                ':payment:uploadArchives'
        ]
    } else {
        // tasks for RELEASE builds, this will also upload to the external packagecloud.io
        printf "Task for RELEASE builds, uploading to Nexus and packagecloud.io."
        tasks = [
                'clean',
                ':payment:test',
                ':payment:assemble',
                ':example-sdk:runFunctionalTest',
                ':example-shop:runFunctionalTest',
                ':payment:uploadArchives',
                ':payment:uploadExternal'
        ]
    }
}

def getCurrentBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}
