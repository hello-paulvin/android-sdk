apply plugin: 'com.android.library'
apply plugin: 'maven'

assemble.dependsOn('lint')

configurations {
    deployerJars
}

android {

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility rootProject.javaCompatVersion
        targetCompatibility rootProject.javaCompatVersion
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        libraryVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "optile-payment-${variant.name}-${defaultConfig.versionName}.aar"
            }
        }
    }
}

dependencies {
    implementation "com.android.support:design:${rootProject.supportLibVersion}"
    implementation "com.google.code.gson:gson:${rootProject.gsonVersion}"
    implementation "com.android.support.test.espresso:espresso-idling-resource:${rootProject.espressoIdlingVersion}@aar"
    implementation("com.github.bumptech.glide:glide:${rootProject.bumptechGlideVersion}") {
        exclude group: "com.android.support"
    }

    testImplementation "io.github.json-snapshot:json-snapshot:${rootProject.jsonsnapshotVersion}"
    testImplementation "junit:junit:${rootProject.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoCoreVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "androidx.test:core:${rootProject.androidxTestCoreVersion}"

    androidTestImplementation "com.android.support.test:runner:${rootProject.runnerVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.espressoCoreVersion}"

    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:${rootProject.packagecloudWagonVersion}"
}

//
// This task contains the upload repositories for Nexus release & shapshot
//
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://nexus.mgmt.oscato.com/repository/releases/") {
                authentication(userName: mavenUser, password: mavenPassword)
                pom.version = versionName
                pom.artifactId = "android-sdk"
                pom.groupId = "com.oscato.mobile"
            }
            snapshotRepository(url: "https://nexus.mgmt.oscato.com/repository/snapshots/") {
                authentication(userName: mavenUser, password: mavenPassword)
                pom.version = versionName
                pom.artifactId = "android-sdk"
                pom.groupId = "com.oscato.mobile"
            }
        }
    }
}

//
// This contains an additional task to upload to external packagecloud.io.
// Run using: gradle uploadExternal
// 
// Documentation about packagecloud gradle at:
// https://packagecloud.io/docs#gradle_deploy
// 
task uploadExternal(type: Upload) {
    configuration = configurations.archives
    uploadDescriptor = true

    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        repository(url: "packagecloud+https://packagecloud.io/optile/repo") {
            authentication(password: packageCloudMavenPassword)
            pom.version = versionName
            pom.artifactId = "android-sdk"
            pom.groupId = "com.oscato.mobile"
        }
    }
}